declare verbose FillGenus, 1;

import "neighbours.mag" : neighbours;

function hecke_primes(rank)
    if rank lt 8 then
        return [2,3,5];
    else
        return [2];
    end if;
end function;

intrinsic StringToReal(s::MonStgElt) -> RngIntElt
{ Converts a decimal string (like 123.456 or 1.23456e40 or 1.23456e-10) to a real number at default precision. }
    if #s eq 0 then return 0.0; end if;
    if "e" in s then
        t := Split(s,"e");
        require #t eq 2: "Input should have the form 123.456e20 or 1.23456e-10";
        return StringToReal(t[1])*10.0^StringToInteger(t[2]);
    end if;
    t := Split(s,".");
    require #t le 2: "Input should have the form 123 or 123.456 or 1.23456e-10";
    n := StringToInteger(t[1]);  s := t[1][1] eq "-" select -1 else 1;
    return #t eq 1 select RealField()!n else RealField()!n + s*RealField()!StringToInteger(t[2])/10^#t[2];
end intrinsic;

function ThetaSeriesIncremental(L, target_prec, timeout)
    best_theta := [];
    best_prec := 0;
    remaining := timeout;
    prec := Maximum(16, Minimum(L) + 4);
    while prec le target_prec and remaining gt 0 do
        current_prec := Minimum(prec, target_prec);
        success, theta, elapsed := TimeoutCall(remaining, ThetaSeries, <L, current_prec - 1>, 1);
        if not success then
            vprintf FillGenus, 1 : "Theta series timed out at precision %o\n", current_prec;
            break;
        end if;
        best_theta := Eltseq(theta[1]);
        best_prec := current_prec;
        vprintf FillGenus, 1 : "Theta series to precision %o in %o s\n", current_prec, elapsed;
        if current_prec ge target_prec then break; end if;
        remaining -:= Ceiling(StringToReal(elapsed));
        prec *:= 2;
    end while;
    return best_theta, best_prec;
end function;

function dict_to_jsonb(dict)
    return "{" * Join([Sprintf("\"%o\":%o", key, dict[key]) : key in Keys(dict)], ",") * "}";
end function;

function to_postgres(val : jsonb_val := false)
    delims := jsonb_val select "[]" else "{}";
    if ISA(Type(val),Mtrx) then
        return to_postgres(Eltseq(val) : jsonb_val:=jsonb_val);
    elif val cmpeq "\\N" then
        return val;
    // I think this is unnecessary, and used to cause problems, so removing for now.
    //elif Type(val) eq MonStgElt then
    //    return "\"" * val * "\""; // This will fail if the string has quotes, but I don't think that's ever true for us.
    elif Type(val) in [SeqEnum, Tup] then
        return delims[1] * Join([Sprintf("%o",to_postgres(x : jsonb_val:=jsonb_val)) : x in val],",") * delims[2];
    elif Type(val) eq Assoc then
        val_prime := AssociativeArray();
        for key in Keys(val) do
            val_prime[to_postgres(key)] := to_postgres(val[key] : jsonb_val:=true);
        end for;
        return dict_to_jsonb(val_prime);
    else
        return val;
    end if;
end function;

function RescaledDualNF(L)
    Q := Rationals();
    K := BaseRing(L);
    B := ChangeRing(BasisMatrix(L),Q);
    M := ChangeRing(InnerProductMatrix(L),Q);
    F := ChangeRing(GramMatrix(L),Q);
    B := F^-1 * B;
    B := IntegralMatrix(B);
    B div:= GCD(Eltseq(B));
    F := B * M * Transpose(B);
    F, d := IntegralMatrix(F);
    g := GCD(Eltseq(F));
    F div:= g;
    M := (d/g) * M;
    return NumberFieldLattice(Rows(ChangeRing(B, K)) : InnerProduct := ChangeRing(M,K));
end function;

function genus_reps_Magma(L)
    // The bound is set to infinity to avoid Magma printing an error message
    // without throwing a runtime error.
    if IsPositiveDefinite(GramMatrix(L)) or (Rank(L) eq 2) then
      return GenusRepresentatives(L : Bound := Infinity());
    end if;
    // due to some bugs in Magma, we convert to number field
    LF := NumberFieldLattice(L);
    reps := GenusRepresentatives(LF);
    return [LatticeWithGram(ChangeRing(GramMatrix(r), Integers()) :
			    CheckPositive := false) : r in reps];
end function;

function genus_reps_Logan(L)
    return Setseq(neighbours(L : thorough));
end function;

function SphereVolume(n)
    RR := RealField();
    pi := Pi(RR);
    m := n div 2;
    if IsEven(n) then
        return pi^m / Factorial(m);
    else
        return 2^n * pi^m * Factorial(m) / Factorial(n);
    end if;
end function;

intrinsic FillGenus(label::MonStgElt : timeout := 1800)
{Fill the data for a genus and its lattice representatives, given files in the genera_basic format.}
    data := Split(Split(Read("genera_basic/" * label), "\n")[1], "|");
    basic_format := Split(Read("genera_basic.format"), "|");
    advanced_format := Split(Read("genera_advanced.format"), "|");
    // This function only fills in basic lattice entries (essentially those that don't require interactions between different genera)
    lat_format := Split(Split(Read("lat_basic.format"), "\n")[1], "|");
    assert #data eq #basic_format;
    basics := AssociativeArray();
    for i in [1..#data] do
        basics[basic_format[i]] := data[i];
        if data[i] eq "None" then basics[basic_format[i]] := "\\N"; end if;
    end for;
    advanced := AssociativeArray();
    lats := [];

    n := StringToInteger(basics["rank"]);
    s := StringToInteger(basics["nplus"]);
    K := Rationals();
    LWG := LatticeWithGram;
    DualLat := Dual;
    rep := basics["rep"];
    // Switch to square brackets
    rep := "[" * rep[2..#rep - 1] * "]"; // Switch to square brackets
    gram0 := Matrix(K, n, eval rep);
    L0 := LWG(gram0 : CheckPositive := false);
    vprintf FillGenus, 1 : "Computing genus representatives...";
    reps := [];
    // Taking care of a special case Magma has trouble with
    genus_success := true;
    if n eq 2 then 
        d := Determinant(L0);
        if IsSquare(-d) then 
            // At the moment, we don't do anything in this case.
            // I think this is always class number 1, but TODO: check!
            genus_success := false;
        end if; 
    end if;
    if genus_success then
        genus_success, reps, elapsed := TimeoutCall(timeout, genus_reps_Magma, <L0>, 1);
        vprintf FillGenus, 1 : "Genus representatives computed in %o seconds\n", elapsed;
    end if;
    advanced["class_number"] := "\\N";
    advanced["adjacency_matrix"] := "\\N";
    advanced["adjacency_polynomials"] := "\\N";
    if genus_success then
        reps := reps[1];
        vprintf FillGenus, 1 : "Number of genus representatives: %o\n", #reps;
        advanced["class_number"] := #reps;
        vprintf FillGenus, 1 : "Computing adjacency matrix for p = ";
        hecke_mats := AssociativeArray();
        hecke_polys := AssociativeArray();
        G := Genus(L0);
        // This works for 2.28 - should be replaced by SetGenus in 2.29
        G`Representatives := reps;
        G`IsNatural := true;
        if (n eq s) then
          for p in hecke_primes(n) do
            vprintf FillGenus, 1 : "%o:", p;
            vtime FillGenus, 1 : Ap := AdjacencyMatrix(G,p);
            fpf := Factorization(CharacteristicPolynomial(Ap));
            hecke_mats[p] := Ap;
            hecke_polys[p] := [(<Coefficients(pair[1]), pair[2]>) : pair in fpf];
          end for;
          vprintf FillGenus, 1 : "Done!\n";
          advanced["adjacency_matrix"] := to_postgres(hecke_mats);
          advanced["adjacency_polynomials"] := to_postgres(hecke_polys);
        end if;
    else
        reps := [];
    end if;
    disc_invs := basics["discriminant_group_invs"];
    disc_invs := "[" * disc_invs[2..#disc_invs-1] * "]"; // Switch to square brackets
    disc_invs := eval disc_invs;
    disc_aut_size := #AutomorphismGroup(AbelianGroup(disc_invs));

    if (n eq s) then
        vprintf FillGenus, 1 : "Computing canonical forms and automorphism groups for representative ";
    end if;

    if (#reps gt 0) then
        to_per_rep := timeout div #reps + 1;
    end if;

    for Li->L in reps do
        lat := AssociativeArray();
        lat["lattice"] := L; // useful for subroutines; removed before saving to disk
        for col in ["rank", "nplus", "nminus", "disc_abs", "disc_sign", "disc_radical", "disc_witt", "disc_geometric", "disc_quadratic", "disc_half", "disc_2adic_unit", "bad_primes", "discriminant_group_invs", "discriminant_group_exponent", "is_even", "level", "scale", "conway_symbol", "dual_conway_symbol"] do
            lat[col] := basics[col];
        end for;
        lat["genus_label"] := basics["label"];
        lat["class_number"] := advanced["class_number"];
        // TODO := The code for ConwaySymbol is currently in sage.
        // The magma implemntation is in version 2.29 that has some bugs
        // This is no longer part of the lattice, only of the genus
        // lat["dual_conway"] := "\\N";
        lat["aut_size"] := "\\N";
        lat["festi_veniani_index"] := "\\N";
        lat["aut_label"] := "\\N";
        lat["aut_group"] := "\\N";
        lat["is_chiral"] := "\\N";
        lat["orthogonal_complement"] := "\\N";
        lat["density"] := "\\N";
        lat["hermite"] := "\\N";
        lat["kissing"] := "\\N";
        lat["minimum"] := "\\N";
        lat["theta_series"] := "\\N";
        lat["theta_prec"] := "\\N";
        lat["successive_minima"] := "\\N";
        // Trying to reduce the size of the entries in the gram matrix
        gram0 := GramMatrix(L);
        gram := LLLGram(gram0);
        max_abs := Max([Abs(x) : x in Eltseq(gram)]);
        max_abs_0 := Max([Abs(x) : x in Eltseq(gram0)]);
        if max_abs_0 le max_abs then
            gram := gram0;
        end if;
        lat["gram"] := Eltseq(gram);
        lat["gram_is_canonical"] := false;
        lat["gram_others"] := []; // This will be manually set in cases like E8 where we want to store other options
        // At the moment we do not have a notion of a canonical gram in the indefinite case
        // !!!  TODO - Need to be able to compute some things for indefinite lattices
        if (n eq s) then 
            // TODO : This is lossy - change later
            vprintf FillGenus, 1 : "%o", gram;
            success, canonical_gram, elapsed := TimeoutCall(to_per_rep, CanonicalForm, <gram>, 1);
            vprintf FillGenus, 1 : "Canonical form computed in %o seconds\n", elapsed;
            if success then 
                canonical_gram := canonical_gram[1];
                lat["gram"] := Eltseq(canonical_gram);
                lat["gram_is_canonical"] := true;
            end if;
            success, aut_group, elapsed := TimeoutCall(to_per_rep, AutomorphismGroupFaster, <L>, 1);
            vprintf FillGenus, 1 : "Automorphism group computed in %o seconds\n", elapsed;
            if success then 
                aut_group := aut_group[1];
                lat["aut_group"] := GroupToString(aut_group : use_id:=false);
                lat["aut_size"] := #aut_group;
                lat["is_chiral"] := &and[Determinant(g) eq 1 : g in Generators(aut_group)];
                // double checking, but also useful for festi-veniani
                LD := Dual(L : Rescale:=false);
                discL, quo := LD/L; 
                disc_aut := AutomorphismGroup(discL);
                assert disc_aut_size eq #disc_aut;
                assert disc_invs eq Invariants(discL);
                gens_disc := [discL.i : i in [1..Ngens(discL)]];
                im_aut := [hom< discL -> discL | [quo(x@@quo*ChangeRing(alpha, Rationals())): x in gens_disc]> : alpha in Generators(aut_group)];
                lat["festi_veniani_index"] := disc_aut_size div #sub<disc_aut | im_aut>;
                if CanIdentifyGroup(#aut_group) then
                    Aid := IdentifyGroup(aut_group);
                    lat["aut_label"] := Sprintf("%o.%o", Aid[1], Aid[2]);
                end if;
            end if;
            lat["density"] := Density(L);
            lat["center_density"] := lat["density"] / SphereVolume(n);
            lat["hermite"] := HermiteNumber(L);
            lat["kissing"] := KissingNumber(L);
            m := Minimum(L);
            lat["minimum"] := m;
            target_prec := Max(150, m+4);
            theta, theta_prec := ThetaSeriesIncremental(L, target_prec, to_per_rep);
            if theta_prec gt 0 then
                lat["theta_series"] := theta;
                lat["theta_prec"] := theta_prec;
            else
                lat["theta_series"] := [1];
                lat["theta_prec"] := 1;
            end if;
            //success, minima, elapsed := TimeoutCall(to_per_rep, SuccessiveMinima, <L>, 2);
            //vprintf FillGenus, 1 : "Successive minima computed in %o seconds\n", elapsed;
            //if success then 
            //lat["successive_minima"] := minima[1]; // For now, we throw away the vecs
            //end if;
            minima, vecs := SuccessiveMinima(L);
            lat["successive_minima"] := minima;

        end if;
        lat["hash"] := "\\N";

        //lat["level"] := Level(LatticeWithGram(ChangeRing(GramMatrix(L), Integers()) : CheckPositive:=false));

        Append(~lats, lat);
    end for;

    vprintf FillGenus, 1 : "Done!\n";

    function cmp_lat(L1, L2)
        if Type(L1["aut_size"]) eq RngIntElt and Type(L2["aut_size"]) eq RngIntElt then
            d := L2["aut_size"] - L1["aut_size"];
            if (d ne 0) then return d; end if;
        end if;
        if Type(L1["theta_series"]) eq SeqEnum and Type(L2["theta_series"]) eq SeqEnum then
            prec := Minimum(L1["theta_prec"], L2["theta_prec"]);
            for i in [1..prec - 1] do
                d := L1["theta_series"][i] - L2["theta_series"][i];
                if (d ne 0) then return d; end if;
            end for;
        end if;
        for i in [1..n^2] do
            d := L1["gram"][i] - L2["gram"][i];
            if (d ne 0) then return d; end if;
        end for;
        return 0;
    end function;

    // Tie breaker
      
    // Need dual_label, dual_conway
    // Compute festi_veniani_index in Sage?
    // Need label for lattice.  Don't want the label to rely on a difficult computation.  So we should probably avoid using the canonical form, and maybe avoid the automorphism group.
    // Proposal: Sort lexicographically by:
    // 1. Size of automorphism group (larger first): unfortunately this may be hard to compute
    // 2. Density
    // 3. theta series
    // 4. dual theta series
    // 5. arbitrary tiebreaker
    // TODO: Sort reps according to canonical form?
    // perm := [1..#lats];
    if (n eq s) then
        Sort(~lats, cmp_lat, ~perm);
    end if;

    SetColumns(0);
    for idx->L in lats do
        // Need label for lattice.
        lats[idx]["label"] := Sprintf("%o.%o", basics["label"], idx);
    end for;

    SetHashes(~lats, ~advanced, theta_elapsed, timeout);

    // TODO: Compute ambient_lattice

    for idx->L in lats do
        lat := L;
        if genus_success and (n eq s) then
            pNeighbors := AssociativeArray();
            for p in hecke_primes(n) do
                // !!! TODO - check that permutation is applied in the right direction
                pNeighbors[p] := ["\"" * lats[j]["label"] * "\"" : j in [1..#lats] | hecke_mats[p][idx^perm,j^perm] ne 0];
            end for;
            lat["pneighbors"] := to_postgres(pNeighbors);
        else
            lat["pneighbors"] := "\\N";
        end if;
        Remove(~lat, "lattice");
        error if Keys(lat) ne Set(lat_format), [k : k in lat_format | k notin Keys(lat)], [k : k in Keys(lat) | k notin lat_format];
        output := Join([Sprintf("%o", to_postgres(lat[k])) : k in lat_format], "|");
        Write("lattice_data/" * lat["label"], output : Overwrite);
    end for;
    error if Keys(basics) ne Set(basic_format), [k : k in basic_format | k notin Keys(basics)], [k : k in Keys(basics) | k notin basic_format];
    error if Keys(advanced) ne Set(advanced_format), [k : k in advanced_format | k notin Keys(advanced)], [k : k in Keys(advanced) | k notin advanced_format];
    output := Join([basics[k] : k in basic_format] cat [Sprintf("%o", advanced[k]) : k in advanced_format], "|");
    Write("genera_advanced/" * label, output : Overwrite);
    return;
end intrinsic;
